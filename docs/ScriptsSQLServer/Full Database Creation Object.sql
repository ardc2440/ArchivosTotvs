IF OBJECT_ID('SP_ERP_CLEANING_DATA_PROCESS', 'P') IS NOT NULL
	DROP PROCEDURE SP_ERP_CLEANING_DATA_PROCESS 

IF EXISTS (SELECT 1 FROM sys.indexes WHERE name = 'IND_ERP_SHIPPING_PROCESS_DETAIL_DOCUMENT_ID' AND object_id = OBJECT_ID('ERP_SHIPPING_PROCESS_DETAIL'))
	DROP INDEX IND_ERP_SHIPPING_PROCESS_DETAIL_DOCUMENT_ID ON ERP_SHIPPING_PROCESS_DETAIL

IF OBJECT_ID('ERP_PURCHASE_ORDER_FILE', 'V') IS NOT NULL
	DROP VIEW ERP_PURCHASE_ORDER_FILE

IF OBJECT_ID('ERP_SALE_ORDER_FILE', 'V') IS NOT NULL
	DROP VIEW ERP_SALE_ORDER_FILE
	
IF OBJECT_ID('ERP_SALE_ORDERS', 'V') IS NOT NULL
	DROP VIEW ERP_SALE_ORDERS

IF OBJECT_ID('ERP_SHIPPING_PROCESS_DETAIL', 'U') IS NOT NULL
	DROP TABLE ERP_SHIPPING_PROCESS_DETAIL

IF OBJECT_ID('ERP_PURCHASE_ORDERS', 'V') IS NOT NULL
	DROP VIEW ERP_PURCHASE_ORDERS

IF OBJECT_ID('ERP_ITEMS', 'V') IS NOT NULL
	DROP VIEW ERP_ITEMS

IF OBJECT_ID('ERP_SHIPPING_PROCESS', 'U') IS NOT NULL
	DROP TABLE ERP_SHIPPING_PROCESS

IF OBJECT_ID('ERP_DOCUMENT_TYPE', 'U') IS NOT NULL
	DROP TABLE ERP_DOCUMENT_TYPE
GO 

CREATE TABLE erp_document_type
(
  DOCUMENT_TYPE_ID SMALLINT NOT NULL,
  LAST_EXECUTION_DATE DATETIME NOT NULL,
  LAST_CLEANING_DATE DATETIME NOT NULL,
  CONSTRAINT PK_ERP_DOCUMENT_TYPE PRIMARY KEY (DOCUMENT_TYPE_ID),
  CONSTRAINT FK_ERP_DOCUMENT_TYPE_DOCUMENT_TYPE FOREIGN KEY (DOCUMENT_TYPE_ID) REFERENCES DOCUMENT_TYPES (DOCUMENT_TYPE_ID)
)
GO	

INSERT INTO ERP_DOCUMENT_TYPE (DOCUMENT_TYPE_ID, LAST_EXECUTION_DATE, LAST_CLEANING_DATE)     
 	 SELECT DOCUMENT_TYPE_ID, GETDATE(),  GETDATE()  
	   FROM DOCUMENT_TYPES
	  WHERE DOCUMENT_TYPE_CODE IN ('O','P')
GO


CREATE VIEW erp_purchase_orders
AS
SELECT IDORDEN PURCHASEID, ACTIONTYPE ACTIONTYPE, FECHACREACION ACTIONDATE 
  FROM (SELECT 'C' ACTIONTYPE, PURCHASE_ORDER_ID IDORDEN, CREATION_DATE FECHACREACION
          FROM PURCHASE_ORDERS
        UNION 
        SELECT 'M', PURCHASE_ORDER_ID, MAX(MODIFICATION_DATE) 
          FROM MODIFIED_PURCHASE_ORDERS
         GROUP BY PURCHASE_ORDER_ID
		UNION 
		SELECT 'X', D.PURCHASE_ORDER_ID, MAX(D.CANCELLATION_DATE) 
          FROM CANCELED_PURCHASE_ORDERS D
         GROUP BY PURCHASE_ORDER_ID) AS ORDENES
 WHERE FECHACREACION > (SELECT A.LAST_EXECUTION_DATE 
						  FROM ERP_DOCUMENT_TYPE A 
						  JOIN DOCUMENT_TYPES B ON B.DOCUMENT_TYPE_ID = A.DOCUMENT_TYPE_ID 
											   AND B.DOCUMENT_TYPE_CODE = 'O')
GO

CREATE TABLE erp_shipping_process
(
	ERP_SHIPPING_PROCESS_ID INT NOT NULL IDENTITY(1,1),
	DESTINATIONPATH VARCHAR(256) NOT NULL,
	EXECUTIONDATE DATETIME NOT NULL,
	CONSTRAINT PK_ERP_SHIPPING_PROCESS PRIMARY KEY (ERP_SHIPPING_PROCESS_ID)
);
GO

CREATE TABLE erp_shipping_process_detail
(
	ERP_SHIPPING_PROCESS_DETAIL_ID INT NOT NULL IDENTITY(1,1),
	ERP_SHIPPING_PROCESS_ID INT NOT NULL ,
	DOCUMENT_TYPE_ID SMALLINT NOT NULL,
	DOCUMENT_ID INT NOT NULL, 
	DOCUMENT_FILE_NAME VARCHAR(30),
	CONSTRAINT PK_ERP_SHIPPING_PROCESS_DETAIL PRIMARY KEY (ERP_SHIPPING_PROCESS_DETAIL_ID),
	CONSTRAINT FK_ERPShippingProcess FOREIGN KEY (ERP_SHIPPING_PROCESS_ID) REFERENCES ERP_SHIPPING_PROCESS (ERP_SHIPPING_PROCESS_ID),
	CONSTRAINT FK_ERPDocumentType FOREIGN KEY (DOCUMENT_TYPE_ID) REFERENCES DOCUMENT_TYPES (DOCUMENT_TYPE_ID)
); 
GO

CREATE INDEX IND_ERP_SHIPPING_PROCESS_DETAIL_DOCUMENT_ID ON ERP_SHIPPING_PROCESS_DETAIL (DOCUMENT_TYPE_ID,DOCUMENT_ID);  
GO

CREATE VIEW erp_items
AS
SELECT ic.REFERENCE_ID IDITEMXCOLOR, l.LINE_CODE CODLINEA, l.LINE_NAME NOMLINEA, i.INTERNAL_REFERENCE REFINTERNA, i.ITEM_NAME NOMITEM, ic.REFERENCE_CODE REFITEMXCOLOR, ic.REFERENCE_NAME NOMCOLOR 
  FROM ITEM_REFERENCES ic
  JOIN ITEMS i ON i.ITEM_ID = ic.ITEM_ID
  JOIN LINES l ON l.LINE_ID = i.LINE_ID
GO 

CREATE VIEW erp_sale_orders
AS
SELECT IDPEDIDO SALEID, ACTIONTYPE, FECHACREACION ACTIONDATE 
  FROM (
        SELECT 'C' ACTIONTYPE , P.CUSTOMER_ORDER_ID IDPEDIDO, P.CREATION_DATE FECHACREACION
          FROM CUSTOMER_ORDERS P         
        UNION  
        SELECT 'M', M.CUSTOMER_ORDER_ID IDPEDIDO, MAX(M.MODIFICATION_DATE) 
          FROM MODIFIED_CUSTOMER_ORDERS M   
		 GROUP BY M.CUSTOMER_ORDER_ID
        UNION 
        SELECT 'X', C.CUSTOMER_ORDER_ID IDPEDIDO, CAST(MAX(C.CANCELLATION_DATE) AS DATETIME)
          FROM CANCELED_CUSTOMER_ORDERS C
         GROUP BY C.CUSTOMER_ORDER_ID
        UNION 
        SELECT 'R', R.CUSTOMER_ORDER_ID IDPEDIDO, MAX(R.CLOSE_DATE) 
          FROM CLOSED_CUSTOMER_ORDERS R
         GROUP BY R.CUSTOMER_ORDER_ID) AS PEDIDOS		 
 WHERE FECHACREACION > (SELECT A.LAST_EXECUTION_DATE 
						  FROM ERP_DOCUMENT_TYPE A 
						  JOIN DOCUMENT_TYPES B ON B.DOCUMENT_TYPE_ID = A.DOCUMENT_TYPE_ID 
												AND B.DOCUMENT_TYPE_CODE = 'P')
GO

CREATE VIEW erp_sale_order_file
AS
SELECT 'P' DocumentType, p.ORDER_NUMBER SaleNumber, t.IDENTITY_TYPE_CODE ClientIdentificationType, c.IDENTITY_NUMBER ClientIdentificationNumber, p.CUSTOMER_NOTES CustomerObservations, p.INTERNAL_NOTES InternalObservations, 
       P.ESTIMATED_DELIVERY_DATE EstimatedDeliveryDate, p.CUSTOMER_ORDER_ID SaleId, ip.CUSTOMER_ORDER_DETAIL_ID SaleDetailId, i.CODLINEA LineCode, i.REFINTERNA ItemCode, i.REFITEMXCOLOR ReferenceCode, ip.DELIVERED_QUANTITY DeliveryQuantity, ip.REQUESTED_QUANTITY RequestedQuantity, s.STATUS_DOCUMENT_TYPE_CODE SaleState
  FROM CUSTOMER_ORDERS p
  JOIN CUSTOMERS c ON c.CUSTOMER_ID = p.CUSTOMER_ID
  JOIN IDENTITY_TYPES t ON t.IDENTITY_TYPE_ID = c.IDENTITY_TYPE_ID
  JOIN CUSTOMER_ORDER_DETAILS ip ON ip.CUSTOMER_ORDER_ID = p.CUSTOMER_ORDER_ID
  JOIN ERP_ITEMS i ON i.IDITEMXCOLOR = ip.REFERENCE_ID  
  JOIN STATUS_DOCUMENT_TYPES s ON s.STATUS_DOCUMENT_TYPE_ID = P.STATUS_DOCUMENT_TYPE_ID
 WHERE EXISTS (SELECT 1 FROM ERP_SALE_ORDERS ep WHERE ep.SaleId = p.CUSTOMER_ORDER_ID)
 GO

CREATE VIEW erp_purchase_order_file
AS
SELECT 'O' DocumentType, o.ORDER_NUMBER PurchaseNumber, t.IDENTITY_TYPE_CODE ProviderIdentificationType, p.IDENTITY_NUMBER ProviderIdentificationNumber, o.EXPECTED_RECEIPT_DATE ArrivingEstimatedDate, o.PROFORMA_NUMBER ProformaNumber, o.PURCHASE_ORDER_ID PurchaseId, io.PURCHASE_ORDER_DETAIL_ID PurchaseDetailId, 
		i.CODLINEA lineCode, i.REFINTERNA ItemCode, i.REFITEMXCOLOR ReferenceCode, io.REQUESTED_QUANTITY Quantity
  FROM PURCHASE_ORDERS o
  JOIN PROVIDERS p ON p.PROVIDER_ID = o.PROVIDER_ID
  JOIN IDENTITY_TYPES t ON t.IDENTITY_TYPE_ID = p.IDENTITY_TYPE_ID
  JOIN PURCHASE_ORDER_DETAILS io ON io.PURCHASE_ORDER_ID = o.PURCHASE_ORDER_ID
  JOIN ERP_ITEMS i ON i.IDITEMXCOLOR = io.REFERENCE_ID
 WHERE EXISTS (SELECT 1 FROM ERP_PURCHASE_ORDERS eo WHERE eo.PurchaseId = o.PURCHASE_ORDER_ID)  
GO

CREATE PROCEDURE sp_erp_cleaning_data_process 
	@LimitDate DATETIME 
AS 
BEGIN
    DELETE FROM ERP_SHIPPING_PROCESS_DETAIL
     WHERE EXISTS (SELECT 1 
                   FROM ERP_SHIPPING_PROCESS 
                  WHERE ERP_SHIPPING_PROCESS_DETAIL.ERP_SHIPPING_PROCESS_ID = ERP_SHIPPING_PROCESS.ERP_SHIPPING_PROCESS_ID
                    AND ERP_SHIPPING_PROCESS.EXECUTIONDATE < @LimitDate);
    
    DELETE 
      FROM ERP_SHIPPING_PROCESS 
     WHERE EXECUTIONDATE < @LimitDate 
                    
    UPDATE ERP_DOCUMENT_TYPE 
       SET LAST_CLEANING_DATE = @LimitDate 
END
GO